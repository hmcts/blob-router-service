plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.2'
  id 'org.owasp.dependencycheck' version '7.4.3'
  id 'com.github.ben-manes.versions' version '0.44.0'
  id 'org.sonarqube' version '3.5.0.2730'
  id 'org.flywaydb.flyway' version '9.11.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

sourceCompatibility = '11'
targetCompatibility = '11'

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

test {
  failFast = false
}

task functional(type: Test) {
  description = "Runs the functional tests."
  group = "Verification"

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs the integration tests."
  group = "Verification"

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  group = "Verification"

  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.53.0"
  ignoreFailures = true
  incrementalAnalysis = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/blobrouter/config/**',
        'uk/gov/hmcts/reform/blobrouter/Application.*' // no need to cover spring boot app entry point
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: blob-router-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:blob-router-service"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property 'sonar.exclusions', "**/config/**,**/data/model/**,**/reconciliation/model/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

configurations.all {
  exclude group: 'org.springframework.security', module: 'spring-security-rsa'
  exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
}

dependencyManagement {
  dependencies {
    // avoid CVE-2021-29425
    dependencySet(group: 'commons-io', version: '2.11.0') {
      entry 'commons-io'
    }

    dependencySet(group: 'org.apache.logging.log4j', version: '2.19.0') {
      entry 'log4j-api'
      entry 'log4j-to-slf4j'
    }
    //CVE-2022-23181
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.70') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-websocket'
    }
    // CVE-2020-36518
    dependencySet(group: 'com.fasterxml.jackson.core', version: '2.14.1') {
      entry 'jackson-databind'
    }
    // CVE-2021-42550
    dependencySet(group: 'ch.qos.logback', version: '1.2.10') {
      entry 'logback-classic'
    }
    //CVE-2022-24823
    dependencySet(group: 'io.netty', version: '4.1.86.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-dns'
      entry 'netty-codec-http'
      entry 'netty-codec-http2'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-resolver-dns'
      entry 'netty-resolver-dns-classes-macos'
      entry 'netty-resolver-dns-native-macos'
      entry 'netty-transport'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-classes-kqueue'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
    }
    //CVE-2022-22970, CVE-2022-22971
    dependencySet(group: 'org.springframework', version: '5.3.24') {
      entry 'spring-aop'
      entry 'spring-beans'
      entry 'spring-context'
      entry 'spring-core'
      entry 'spring-expression'
      entry 'spring-jcl'
      entry 'spring-context-support'
      entry 'spring-jdbc'
      entry 'spring-tx'
      entry 'spring-web'
      entry 'spring-webmvc'
    }
    // CVE-2022-25857
    dependencySet(group: 'org.yaml', version: '1.33') {
      entry 'snakeyaml'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

def versions = [
  junit           : '5.9.1',
  junitPlatform   : '1.9.1',
  reformLogging   : '6.0.1',
  apiguardian     : '1.1.2'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}",
    "org.apiguardian:apiguardian-api:${versions.apiguardian}"
  ]
]
ext["rest-assured.version"] = '4.2.1'

dependencies {
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.1'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '8.5.13'

  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.44.0'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.44.0'

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.19.1'
  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.13.0'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.5'
  // region: feign clients
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.5'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '12.1'
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '12.1'
  // end region

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.14'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.2'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.2'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'

  implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.9.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'org.apache.commons', name: 'commons-email', version: '1.5'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0', classifier: 'all'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.5'

  integrationTestImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.33.2'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.17.6', {
    exclude group: 'junit', module: 'junit'
  }
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.17.6'
  integrationTestImplementation group: 'com.icegreen', name: 'greenmail', version: '1.6.12'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation libraries.junit5
  functionalTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
  functionalTestImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  functionalTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.2'
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation libraries.junit5
  smokeTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
  smokeTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.2'
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }
}

mainClassName = 'uk.gov.hmcts.reform.blobrouter.Application'

bootJar {
  getArchiveFileName().set(provider {
    'blob-router-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  user = System.getenv('DB_USER')
  password = System.getenv('DB_PASSWORD')

  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) { // set by jenkins
    url = "jdbc:postgresql://${dburl}"
  }
}

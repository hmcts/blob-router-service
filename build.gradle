buildscript {
  dependencies {
    classpath("org.postgresql:postgresql:42.7.3")
    classpath("org.flywaydb:flyway-database-postgresql:10.13.0")
  }
}
plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'org.springframework.boot' version '3.2.7'
  id 'org.owasp.dependencycheck' version '9.0.10'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'org.sonarqube' version '5.0.0.4638'
  id 'org.flywaydb.flyway' version '10.13.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

test {
  failFast = false
}

task functional(type: Test) {
  description = "Runs the functional tests."
  group = "Verification"

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs the integration tests."
  group = "Verification"

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  group = "Verification"

  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.3'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.55.0"
  ignoreFailures = true
  incrementalAnalysis = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required.set(true)
    csv.required.set(false)
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/blobrouter/config/**',
        'uk/gov/hmcts/reform/blobrouter/Application.*' // no need to cover spring boot app entry point
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: blob-router-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:blob-router-service"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property 'sonar.exclusions', "**/config/**,**/data/model/**,**/reconciliation/model/**,**/jms/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

configurations.all {
  exclude group: 'org.springframework.security', module: 'spring-security-rsa'
  exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

def versions = [
  junit           : '5.10.1',
  junitPlatform   : '1.10.1',
  reformLogging   : '6.1.4',
  apiguardian     : '1.1.2'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}",
    "org.apiguardian:apiguardian-api:${versions.apiguardian}"
  ]
]

dependencies {
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '10.13.0'

  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '5.14.0'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '5.14.0'

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.25.0'
  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.17.2'

  implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: '1.11.0'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.1.3'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq'
  // region: feign clients
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.2'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '13.3'
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.3'
  // end region

  implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.3.1'
  implementation group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'
  implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0' // until logging-appinsights supports SB 3.x

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.5.0'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'

  implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.11.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
  implementation group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.3', classifier: 'all'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '4.1.3'

  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.19.3', {
    exclude group: 'junit', module: 'junit'
  }
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.19.3'
  integrationTestImplementation group: 'com.icegreen', name: 'greenmail', version: '2.1.0-rc-1'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation libraries.junit5
  functionalTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
  functionalTestImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  functionalTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.3'
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation libraries.junit5
  smokeTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
  smokeTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.3'
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }

  runtimeOnly group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.13.0'
}

mainClassName = 'uk.gov.hmcts.reform.blobrouter.Application'

bootJar {
  getArchiveFileName().set(provider {
    'blob-router-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  user = System.getenv('DB_USER')
  password = System.getenv('DB_PASSWORD')

  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) { // set by jenkins
    url = "jdbc:postgresql://${dburl}"
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.springframework.boot' version '2.2.4.RELEASE'
  id 'org.owasp.dependencycheck' version '5.3.0'
  id 'com.github.ben-manes.versions' version '0.27.0'
  id 'org.sonarqube' version '2.8'
  id 'org.flywaydb.flyway' version '6.2.1'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

sourceCompatibility = '11'
targetCompatibility = '11'

sourceSets {
  apiGatewayTest {
    java {
      srcDir file('src/apiGatewayTest/java')
    }
    resources.srcDir file('src/apiGatewayTest/resources')
  }

  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

test {
  failFast = true
}

task apiGateway(type: Test) {
  description = "Runs API Gateway Tests"
  group = "Verification"

  testClassesDirs = sourceSets.apiGatewayTest.output.classesDirs
  classpath = sourceSets.apiGatewayTest.runtimeClasspath
}

task functional(type: Test) {
  description = "Runs the functional tests."
  group = "Verification"

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs the integration tests."
  group = "Verification"

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  group = "Verification"

  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.26'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.19.0"
  ignoreFailures = true
  incrementalAnalysis = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/blobrouter/config/**',
        'uk/gov/hmcts/reform/blobrouter/Application.*' // no need to cover spring boot app entry point
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: blob-router-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:blob-router-service"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    property 'sonar.exclusions', "**/config/**,**/data/model/**"

  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependencySet(group: 'com.google.guava', version: '28.2-jre') {
      entry 'guava'
    }
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.30') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-websocket'
    }
  }
}

repositories {
  jcenter()
}

def versions = [
  junit           : '5.6.0',
  junitPlatform   : '1.6.0',
  reformLogging   : '5.1.1',
  springBoot      : springBoot.class.package.implementationVersion
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]
ext["rest-assured.version"] = '4.1.2'

dependencies {
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.9'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '6.1.4'

  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.2.0'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.2.0'

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.3.0'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'

  // region: feign clients
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.2.1.RELEASE'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.4.0'
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '10.4.0'
  // end region

  // following includes web, actuator and validator.
  // until library is updated, can safely remove extra definition on our own ^
  implementation group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: '0.0.5'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9' // can be removed once openapi is used
//  implementation group: 'org.springdoc', name: 'springdoc-openapi-webmvc-core', version: '1.2.26'

  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging

  implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'com.azure', name: 'azure-core-test', version: '1.1.0', {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-standalone'
  }

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'com.azure', name: 'azure-core-test', version: '1.1.0', {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-standalone'
  }
  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.2.1.RELEASE', {
    exclude group: 'com.github.tomakehurst', module: 'wiremock-jre8-standalone'
  }
  integrationTestImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.25.1'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.12.4'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation libraries.junit5
  functionalTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.15.0'
  functionalTestImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  functionalTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.0'
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation libraries.junit5
  smokeTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.14.0'
  smokeTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.0'
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }

  apiGatewayTestImplementation libraries.junit5
  apiGatewayTestImplementation group: 'com.typesafe', name: 'config', version: '1.4.0'
  apiGatewayTestImplementation group: 'io.rest-assured', name: 'rest-assured', {
    exclude group: 'junit', module: 'junit'
  }
  apiGatewayTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.14.0'
}

mainClassName = 'uk.gov.hmcts.reform.blobrouter.Application'

bootJar {
  getArchiveFileName().set(provider {
    'blob-router-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) { // set by jenkins
    url = "jdbc:postgresql://${dburl}"
  }
}
